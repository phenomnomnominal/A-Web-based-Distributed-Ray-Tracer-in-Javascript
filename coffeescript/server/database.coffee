# *database.coffee* provides all database functionality, such as inserting render data in the [NoSQL](http://en.wikipedia.org/wiki/NoSQL) database and later retrieving it.
# ___

# ## Requires:
# Functionality in *database.coffee* requires access to the following [**node.js**](http://nodejs.org/) modules:

# * [**MongoDB**](http://www.mongodb.org/) - Scalable, High-Performance [**NoSQL**](http://en.wikipedia.org/wiki/NoSQL) database.
mongo = require 'mongodb'

# ___

# ## Constants:
# Some constants are required for this script:

# > * **`LOCATION`** - *IP Address location for the server*
LOCATION = '127.0.0.1'

# > * **`PORT`** - *Port for database connections*
PORT = 27017

# > * **`DATABASE_NAME`** - *Name of the database to connect to*
DATABASE_NAME = 'renderDB'

# ___

# ## Initialisation:
# When [*index.coffee*](index.html) runs and the [**node.js**](http://nodejs.org/) server is initialised, this script is run, which initialises the [**MongoDB**](http://www.mongodb.org/) [**`Db`**](http://mongodb.github.com/node-mongodb-native/api-generated/db.html) and opens a connection to it.
server = new mongo.Server LOCATION, PORT, autoconnect: true
db = new mongo.Db DATABASE_NAME, server, {}
db.open (error, connection) ->
  console.log if error? then "Error: #{error.message}" else "Connected to #{DATABASE_NAME}"
  
# ___

# ## Functions:

# ### <section id='insert'>*InsertRenderIntoDatabase*:</section>
# > **`InsertRenderIntoDatabase`** takes a `renderID` (a [**UUID**](http://en.wikipedia.org/wiki/Universally_unique_identifier) generated by [*uuid.js*](https://github.com/LiosK/UUID.js)) and the parsed `render` object and stores them as a new [**`collection`**](http://mongodb.github.com/node-mongodb-native/api-generated/collection.html) in the database.
InsertRenderIntoDatabase = (renderID, render) ->
  db.createCollection renderID, safe:true, (error, collection) ->
    console.log if error? then "Error: #{error.message}" else "Collection created: #{collection}"
    unless error?
      db.collection renderID, (error, collection) ->
        console.log if error? then "Error: #{error.message}" else collection
        unless error?
          collection.insert render, safe: true, (error, result) ->
            console.log if error? then "Error: #{error.message}" else "Success: #{result}"

# ### <section id='find'>*FindRenderInDatabase*:</section>
# > **`FindRenderInDatabase`** searches for a `render` object that is assosciated with a given `renderID` using [**`collection.findOne`**](http://mongodb.github.com/node-mongodb-native/api-generated/collection.html#findone) and returns the object (if one is found) via [**WebSocket**](http://www.websocket.org/) to the client with the correct `sessionID`.
FindRenderInDatabase = (renderID, sessionID) ->
  db.collection renderID, (error, collection) ->
    console.log if error? then "Error: #{error.message}" else collection
    unless error?
      collection.findOne uuid: renderID, (error, render) ->
        console.log if error? then "Error: #{error.message}" else render
        unless error?
          server = require './server'
          (server.io.sockets.in sessionID).emit 'gotRender', render: render

# ___
# ## Exports

# The [**`InsertRenderIntoDatabase`**](#insert) and [**`FindRenderInDatabase`**](#find) functions are added to the global `root` object.
root = exports ? this
root.insert = InsertRenderIntoDatabase
root.find = FindRenderInDatabase